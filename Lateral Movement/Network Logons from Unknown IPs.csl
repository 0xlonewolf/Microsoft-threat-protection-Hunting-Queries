////////////////////////////////////////////////////////////////////
// Network Logons from Unknown IP Addresses
//
// This query identifies network logons from IP addresses not associated with any machine in Defender ATP
////////////////////////////////////////////////////////////////////
DeviceLogonEvents
| where LogonType == 'Network' and isnotempty(RemoteDeviceName) and isnotempty(RemoteIP) and RemoteIP != '::1' and RemoteIP !startswith "127." 
| summarize EarliestEvent = min(Timestamp), LatestEvent = max(Timestamp), Instances = count(), DistinctMachines = dcount(DeviceId) by AccountDomain, AccountName, RemoteIP, ActionType
| join kind=leftanti (
    DeviceNetworkInfo
    | project NetworkInfo = todynamic(IPAddresses )
    | mvexpand NetworkInfo
    | project IpAddress = tostring(parse_json(NetworkInfo).IPAddress)
    | distinct IpAddress
) on $left.RemoteIP == $right.IpAddress
