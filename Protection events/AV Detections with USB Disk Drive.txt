//This query make a best-guess detection regarding which removable media device caused an AV detection
//The query is best run over 30 days to get the full USB history
//
//Get a list of USB AV detections. This assumes any path not beginning with C is a removable/USB device
let usbDetections =
    MiscEvents
    | where ActionType == "AntivirusDetection" and FolderPath !startswith "c" and FolderPath matches regex "^[A-Za-z]{1}"
    | extend ParsedFields=parse_json(AdditionalFields)
    | project DetectionTime=EventTime, ComputerName, ThreatName=tostring(ParsedFields.ThreatName), FileName, FolderPath;
//Get a list of USB disk drive connections, grouped by computer name and DeviceID
let usbConnections = 
    MiscEvents
    | where ActionType == "PnpDeviceConnected"
    | extend parsed=parse_json(AdditionalFields)
    | project EventTime, ComputerName, DeviceId=tostring(parsed.DeviceId), ClassName=tostring(parsed.ClassName)
    | where ClassName == "DiskDrive"
    | summarize UsbFirstSeen=min(EventTime), UsbLastSeen=max(EventTime) by DeviceId, ComputerName;
//Join USB AV detections and connections, where the detection occurs after the USB has been plugged in
usbDetections | join kind=inner (usbConnections) on ComputerName | where DetectionTime > UsbFirstSeen and DetectionTime < UsbLastSeen
| project DetectionTime, ComputerName, ThreatName, FileName, FolderPath, DeviceId, UsbFirstSeen, UsbLastSeen
| sort by DetectionTime desc