// Establishes a baseline SystemGuardSecurityLevel and show the devices that are below that baseline
// For more questions on this query, feel free to ping @DepletionMode or @flyingbluemonki on twitter
let TargetSecurityLevel = 700;
MiscEvents
| where EventTime >= ago(30d)
| where ActionType == "DeviceBootAttestationInfo"
| extend AdditionalFieldData = parse_json(AdditionalFields)
| project ComputerName, ReportTime = todatetime(AdditionalFieldData.ReportValidityStartTime), CurrentSecurityLevel = toint(AdditionalFieldData.SystemGuardSecurityLevel), AdditionalFieldData.ReportValidityStartTime
| where CurrentSecurityLevel < TargetSecurityLevel
| summarize arg_max(ReportTime, CurrentSecurityLevel) by ComputerName