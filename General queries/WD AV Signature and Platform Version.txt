// Author: AntÃ³nio Vasconcelos
// Twitter: https://twitter.com/anthonws
// --------------------------------------------------------------------------------------------------------------------------- //
// *GOAL*
// This query provides you the latest signature and platform (MoCamp) for Windows Defender AV per DeviceName.
// If a given machine does not have one of the fields populated, it means that AH does not have data for it, in the last 30 days.
// The main objective with this query is being able to reportin on a specific or group of devices, during an IR.
// Please note that AH has a limitation of 10K rows output per query!
// THIS QUERY SHOULD NOT REPLACE A ROBUST REPORTING MECHANISM, OFFERED BY PLATFORMS LIKE MICROSOFT ENDPOINT MANAGER!
// --------------------------------------------------------------------------------------------------------------------------- //
// *DISCLAIMER*
// THIS IS A SAMPLE QUERY
// PLEASE NOTE THAT I TAKE NO RESPONSIBILITY ON THE RESULTS THIS QUERY MIGHT YIELD
// YOU SHOULD TEST IT AND MAKE SURE IT FITS YOUR NEEDS 
// THIS QUERY IS NOT OFFICIAL AND ENDORSED BY MICROSOFT
// --------------------------------------------------------------------------------------------------------------------------- //
// *CHANGELOG*
// V4.1 - 15JUN2020
// - Fixed bug in arg_max() (thanks jmmowrer)
// - Changed join to fullouter to ensure completeness of results (thanks jmmowrer)
// V4 - 07MAY2020
// - Simplified logic, updated fields to match PowerShell Get-MpComputerStatus (mjmelone)
// V3 - 20/04/2020
// - Correct a "bug" in the summarize
// - Improved stability
// - Added disclaimer
// V2 - 19/22/2019
// - Changed the query to account for the time separation between Signature Updates and Engine Updates.
// - Should now reflect properly both Signature and Enginer per machine, in a single line per device.
// V1 - Long, long time ago
// - Initial query
// --------------------------------------------------------------------------------------------------------------------------- //
// How far back to search
let StartDate = ago(30d);
DeviceFileEvents 
| where Timestamp > StartDate
// Find signature \ engine update activity
| where InitiatingProcessFileName =~ 'MpSigStub.exe' and InitiatingProcessCommandLine contains '/stub' and InitiatingProcessCommandLine contains '/payload'
| summarize Timestamp = arg_max(Timestamp, InitiatingProcessCommandLine) by DeviceId, DeviceName
| extend SplitCommand = split(InitiatingProcessCommandLine, ' ')
// Locate stub and payload versions
| extend EngineVersionLocation = array_index_of(SplitCommand, "/stub") + 1, DefinitionVersionLocation = array_index_of(SplitCommand, "/payload") + 1
| project Timestamp, DeviceName, DeviceId, AMEngineVersion = SplitCommand[EngineVersionLocation], AntivirusSignatureVersion = SplitCommand[DefinitionVersionLocation]
| join kind=fullouter (
    DeviceProcessEvents
    | where Timestamp > StartDate
    // Find process creations for MsMpEng from the platform folder
    | where FileName =~ 'MsMpEng.exe' and FolderPath contains @"\Microsoft\Windows Defender\Platform\"
    | summarize arg_max(Timestamp, FolderPath) by DeviceId, DeviceName
    // Go up two levels
    | project DeviceId, DeviceName, AMServiceVersion = split(FolderPath, '\\')[-2]
) on DeviceId
// Re-projecting to make the UI happy
| project DeviceId, DeviceName, AMEngineVersion, AntivirusSignatureVersion, AMServiceVersion
